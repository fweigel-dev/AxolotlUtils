plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"axolotlutils" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

sourceSets {
	clientTest {
		compileClasspath += sourceSets.client.output + sourceSets.main.output
		runtimeClasspath += sourceSets.client.output + sourceSets.main.output
	}
}

configurations {
	clientTestImplementation.extendsFrom clientImplementation, implementation
	clientTestRuntimeOnly.extendsFrom clientRuntimeOnly, runtimeOnly
}

dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings loom.officialMojangMappings()
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

        clientTestImplementation "org.junit.jupiter:junit-jupiter:5.11.4"
        clientTestRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.register('clientTest', Test) {
	group = 'verification'
	testClassesDirs = sourceSets.clientTest.output.classesDirs
	classpath = sourceSets.clientTest.runtimeClasspath
	useJUnitPlatform()
}

check.dependsOn clientTest

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {
	}
}
